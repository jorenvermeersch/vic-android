import org.gradle.internal.os.OperatingSystem

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs'
    id 'org.jetbrains.kotlin.kapt'
    id 'org.jlleitschuh.gradle.ktlint'
}

ktlint {
    verbose = true
}

android {
    namespace 'com.example.vic'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.vic"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "demo"]
    }
    buildFeatures {
        dataBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Timber.
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Navigation.
    implementation "androidx.navigation:navigation-fragment-ktx:$version_navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$version_navigation"

    // Room database.
    implementation "androidx.room:room-runtime:$room_version"
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha04'
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Auth0
    implementation 'com.auth0.android:auth0:2.+'

    // Base.
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}

task installGitHook(type: Copy) {
    OperatingSystem os = OperatingSystem.current()
    def suffix = "macos"

     if (os.isWindows()) {
         suffix = "windows"
     }

    from new File(rootProject.rootDir, "scripts/pre-commit-$suffix")
    into { new File(rootProject.rootDir, '.git/hooks') }
    rename("pre-commit-$suffix", 'pre-commit')

    fileMode 0775
}

tasks.getByPath(':app:preBuild').dependsOn installGitHook